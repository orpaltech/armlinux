# qmake configuration for the Allwinner sun8i SoCs

include(../common/linux_device_pre.conf)

QT_QPA_DEFAULT_PLATFORM	= eglfs

MALI_LIB_PATH	= /opt/mali/lib
MALI_INC_PATH	= /opt/mali/include

QMAKE_INCDIR_POST	+= $$[QT_SYSROOT]$${MALI_INC_PATH}
QMAKE_LIBDIR_POST	+= $$[QT_SYSROOT]$${MALI_LIB_PATH}
QMAKE_RPATHLINKDIR_POST	+= $$[QT_SYSROOT]$${MALI_LIB_PATH}

QMAKE_LIBDIR_OPENGL_ES2	= $${MALI_LIB_PATH}
QMAKE_LIBDIR_EGL	= $${QMAKE_LIBDIR_OPENGL_ES2}

QMAKE_INCDIR_EGL	= $${MALI_INC_PATH}
QMAKE_INCDIR_OPENGL_ES2	= $${QMAKE_INCDIR_EGL}

QMAKE_LIBS_OPENGL_ES2	= -Wl,-rpath,$${MALI_LIB_PATH} -lGLESv2
QMAKE_LIBS_EGL		= -Wl,-rpath,$${MALI_LIB_PATH} -lEGL

COMPILER_FLAGS	+= -march=armv7-a -marm -mthumb-interwork -mfpu=vfpv4 -mtune=cortex-a7
QMAKE_LIBS	+= -lrt -lpthread -ldl -lz

DISTRO_OPTS	+= hard-float
DISTRO_OPTS	+= deb-multi-arch

EGLFS_DEVICE_INTEGRATION = eglfs_mali

include(../common/linux_arm_device_post.conf)

load(qt_config)
